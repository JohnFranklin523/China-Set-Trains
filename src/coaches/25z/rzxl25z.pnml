//Begin RZXL25Z
switch (FEAT_TRAINS, SELF, switch_rzxl25z_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25Z_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_rzxl25z_purchase_original, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25z","rzxl25z")
}

// Vehicle
spriteset (spriteset_rzxl25z_original, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25z","rzxl25z")
}
spriteset (spriteset_rzxl25z_alternative, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25z","rzxl25z-guangzhou-sifang-a")
}
/* spriteset (spriteset_rzxl25z_green, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25z","rzxl25z-guangzhou-green")
}
 */
switch (FEAT_TRAINS, SELF, switch_rzxl25z_articulated_part, extra_callback_info1) {
    1: return rzxl25z;
    2: return luggageroom_25;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_rzxl25z_cargo_subtype, cargo_subtype) {
    0: return string(STR_GUANGSHEN_CHANGKE_1);
    1: return string(STR_GUANGSHEN_CHANGKE_2);
    /* 2: return string(STR_GUANGSHEN_GREEN); */
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_rzxl25z_graphics_subtype, cargo_subtype) {
    0: spriteset_rzxl25z_original;
    1: spriteset_rzxl25z_alternative;
/*     2: spriteset_rzxl25z_green; */
}

switch (FEAT_TRAINS, SELF, switch_rzxl25z_graphics, position_in_articulated_veh % 3) {
    1: switch_rzxl25z_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_rzxl25z_capacity, position_in_articulated_veh % 3) {
    1: return 56;
    return 0;
}


item (FEAT_TRAINS, rzxl25z) {
    property {
        // Menu
        name:                           string(STR_NAME_RZXL25Z);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2099;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01);
        model_life:                     30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 71;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         42 ton;
        cost_factor:                    7;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            4;
    }

    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_RZXL25Z), string(STR_LV_AVAILABLE_L ,string(STR_LIVERY_AVAILABLE), string(STR_GUANGSHEN)/* , string(STR_GUANGSHEN_GREEN) */), string(STR_FRESHNESS_MEDIUM, 200));
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_rzxl25z_graphics;
        purchase:                       spriteset_rzxl25z_purchase_original;
        articulated_part:               switch_rzxl25z_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_rzxl25z_capacity;
        cargo_age_period:               (20 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    8;
        running_cost_factor:            6 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   24;
        purchase_cargo_capacity:        56/2;
        name:                           switch_rzxl25z_name;

    }
}

// End RZXL25Z
