//Begin SCA25KHD

// Graphics

// Purchase Menu
spriteset (spriteset_sca25khd_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25k/sca25khd-blue-red")
}

// Vehicle
spriteset (spriteset_sca25khd_blue_red, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25k/sca25khd-blue-red")
}
spriteset (spriteset_sca25khd_green, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25k/sca25khd-green")
}
spriteset (spriteset_sca25khd_shenyang, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25k/sca25khd-shenyang")
}
spriteset (spriteset_sca25khd_red, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25k/sca25khd-red")
}


switch (FEAT_TRAINS, SELF, switch_sca25khd_articulated_part, extra_callback_info1) {
    1..2: return sca25khd;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_sca25khd_cargo_subtype, cargo_subtype) {
    0: return string(STR_BLUE_RED_WHITE);
    1: return string(STR_GAOZU_GREEN);
    2: return string(STR_SHENYANG);
    3: return string(STR_RED);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_sca25khd_graphics_subtype, cargo_subtype) {
    0: spriteset_sca25khd_blue_red;
    1: spriteset_sca25khd_green;
    2: spriteset_sca25khd_shenyang;
    3: spriteset_sca25khd_red;
}

switch (FEAT_TRAINS, SELF, switch_sca25khd_graphics, position_in_articulated_veh % 3) {
    1: switch_sca25khd_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_sca25khd_capacity, position_in_articulated_veh % 3) {
    1: return 72;
    return 0;
}


item (FEAT_TRAINS, sca25khd) {
    property {
        // Menu
        name:                           string(STR_NAME_SCA25KHD);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					syz25kld;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1999,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         54.3 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(1);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_5, string(STR_FULL_NAME_SCA25K), string(STR_AIR_CONDITIONER_NEED_POWER), string(STR_LV_AVAILABLE_S , string(STR_LIVERY_AVAILABLE), string(STR_BLUE_RED_WHITE), string(STR_GAOZU_GREEN), string(STR_SHENYANG), string(STR_RED)), string(STR_COMFORT_MEDIUM, 200), string(STR_CAFE_EFFECT));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_sca25khd_cargo_subtype;

        // Graphics
        default:                        switch_sca25khd_graphics;
        purchase:                       spriteset_sca25khd_purchase;
        articulated_part:               switch_sca25khd_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_sca25khd_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    28;
        running_cost_factor:            6 * runningcostfactor() + accostfactor() + 50;
        purchase_running_cost_factor:   74;
        purchase_cargo_capacity:        72/3;
        name:                           switch_s25k_name;

    }
}

// End SCA25KHD

