//Begin SS4C
switch (FEAT_TRAINS, SELF, switch_ss4c_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_SS4_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_ss4c_purchase_original, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_purchase ("25-electric", "ss4c-front")
}

// Vehicle
spriteset (spriteset_ss4c_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss4c-front")
}
spriteset (spriteset_ss4c_back, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("25-electric", "ss4c-back")
}

switch (FEAT_TRAINS, SELF, switch_ss4c_articulated_part, extra_callback_info1) {
    1..5: return ss4c;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ss4c_graphics, position_in_articulated_veh % 6) {
    1: spriteset_ss4c_front;
    4: spriteset_ss4c_back;
    empty_sprites;
}


item (FEAT_TRAINS, ss4c) {
    property {
        // Menu
        name:                           string(STR_NAME_SS4C);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        variant_group:					6150;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1997,01,01);
        model_life:                     60;
        retire_early:                   30;
        vehicle_life:                   30;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          6400 kW;
        weight:                         200 ton;
        tractive_effort_coefficient:    0.348;
        cost_factor:                    127;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            104;
        reliability_decay:              24;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_SS4C), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELDEC_LOW, 8));
        can_attach_wagon:               locowagon;
        // Graphics
        default:                        switch_ss4c_graphics;
        purchase:                       spriteset_ss4c_purchase_original;
        articulated_part:               switch_ss4c_articulated_part;

        // Stats
        length:                         switch_length_2_4_2;
        cargo_capacity:                 0;
        cost_factor:                    110;
        running_cost_factor:            93 * runningcostfactor();
        purchase_running_cost_factor:   372;
        purchase_cargo_capacity:        0;
        bitmask_vehicle_info:           0;
        name:                            switch_ss4c_name;

    }
}

// End SS4C

