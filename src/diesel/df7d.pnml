// Begin DF7D
switch (FEAT_TRAINS, SELF, switch_df7d_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_DF7D_SERIES);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_df7d_purchase_original, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_purchase ("diesel", "df7d")
}

// Vehicle
spriteset (spriteset_df7d_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df7d")
}
spriteset (spriteset_df7d_back, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_reversed_10 ("diesel", "df7d")
}
spriteset (spriteset_df7d_green_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_standard ("diesel", "df7d-green")
}
spriteset (spriteset_df7d_green_back, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP) {
    template_reversed_10 ("diesel", "df7d-green")
}

switch (FEAT_TRAINS, SELF, switch_df7d_articulated_part, extra_callback_info1) {
    1..5: return df7d;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_df7d_cargo_subtype, cargo_subtype) {
    0: return string(STR_ORANGE);
    1: return string(STR_GREEN);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_df7d_orange_livery_graphics, position_in_articulated_veh % 6) {
    1: spriteset_df7d_front;
    4: spriteset_df7d_back;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_df7d_green_livery_graphics, position_in_articulated_veh % 6) {
    1: spriteset_df7d_green_front;
    4: spriteset_df7d_green_back;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_df7d_graphics_subtype, cargo_subtype) {
    0: switch_df7d_orange_livery_graphics;
    1: switch_df7d_green_livery_graphics;
}

switch (FEAT_TRAINS, SELF, switch_df7d_graphics, position_in_articulated_veh % 3) {
    1: switch_df7d_graphics_subtype;
    empty_sprites;
}


item (FEAT_TRAINS, df7d) {
    property {
        // Menu
        name:                           string(STR_NAME_DF7D);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        engine_class:                   ENGINE_CLASS_DIESEL;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1995,06,01);
        model_life:                     60;
        retire_early:                   30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 1;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          100 km/h;
        power:                          4000 kW;
        weight:                         276 ton;
        tractive_effort_coefficient:    0.316;
        cost_factor:                    53;
        running_cost_base:              RUNNING_COST_DIESEL;
        running_cost_factor:            72;
        reliability_decay:              32;
        bitmask_vehicle_info:           0;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_DF7D), string(STR_ELECTRICITY_SUPPLY_NO), string(STR_RELDEC_MEDIUM, 8));
        cargo_subtype_text:             switch_df7d_cargo_subtype;
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_df7d_graphics;
        purchase:                       spriteset_df7d_purchase_original;
        articulated_part:               switch_df7d_articulated_part;

        // Stats
        length:                         switch_length_2_6_2;
        cargo_capacity:                 0;
        cost_factor:                    104;
        running_cost_factor:            67 * runningcostfactor();
        purchase_running_cost_factor:   268;
        purchase_cargo_capacity:        0;
        name:                           switch_df7d_name;
    }
}

// End DF7D

